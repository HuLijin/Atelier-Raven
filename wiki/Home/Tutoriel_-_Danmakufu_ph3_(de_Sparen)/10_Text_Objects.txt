Content-Type: text/x-zim-wiki
Wiki-Format: zim 0.4
Creation-Date: 2015-09-01T17:27:15+02:00

====== 10 Text Objects ======

Did you hate 0.12m's ugly DrawText? Well say bye to that ugly horror!

ph3 brings in Text Objects. They are MUCH more customizable, not ugly at all, and can support a number of different fonts! YES! In ph3, you can use your own fonts (if they are supported)!

 task CutinText(strText){
	 let objtext = ObjText_Create();
	 ObjText_SetText(objtext, strText);
	 Obj_SetRenderPriority(objtext, 0.74);//Render above cutin
	 ObjText_SetFontSize(objtext, 16);
	 ObjText_SetFontColorTop(objtext, 255, 64, 64);
	 ObjText_SetFontColorBottom(objtext, 255, 64, 64);
	 ObjText_SetFontBold(objtext, true);
	 ObjText_SetFontBorderType(objtext, BORDER_FULL);
	 ObjText_SetFontBorderColor(objtext, 0, 200, 200);
	 ObjText_SetFontBorderWidth(objtext, 1);
	 ObjRender_SetPosition(objtext, GetStgFrameLeft+5, GetStgFrameHeight-30, 0);
	 loop(300){yield;}
	 Obj_Delete(objtext);
 }
Above is the text I used for my spell cut in. You create a text object, set its render priority, set its font size, set font color, etc. The border is a full border around it, as seen in #Text when selecting players. Note that they are Render objects, and note that there are many more functions that I did not use for this particular task.

Now you can ditch 0.12m DrawText and use this far-superior one. ^_^

(Do note that text can be a little bit of a pain to position)

Linked is a list of Text Object functions. Text Object Functions

NOTE: Scripts made in ph3 [.0 pre23] and before may have text compatibility issues with ph3 [.1 pre2] and later. It is highly recommended that you install your own font so that you know if your words are being chopped in half and thrown onto different lines. Please note that InstallFont returns a boolean value; Make sure your font is actually being installed successfully.

===== SetHorizontalAlignment =====

In this section, I will discuss usage of ObjText_SetHorizontalAlignment(obj, char), where char is one of the following:

ALIGNMENT_CENTER
ALIGNMENT_RIGHT
ALIGNMENT_LEFT
Firstly, you MUST use ObjText_SetMaxWidth. Not using this will send your text flying off the screen.

For an example, I will be centering the Spellcard Bonus text. task TGainSpell(score){

		let objText = ObjText_Create();
	   ObjText_SetText(objText, "ExAttack Bonus!");
	   //Code removed
	   ObjText_SetHorizontalAlignment(objText, ALIGNMENT_CENTER);
	   ObjText_SetMaxWidth(objText, 240);
	   ObjRender_SetX(objText, 194-120);//Minus half the max width. 194 = 388/2 (388 is GetStgFrameWidth w/ r.p.<80)
	   ObjRender_SetY(objText, 98);
	   //etc.

Look at ObjRender_SetX. Here, I have 194 pixels, which is 388 (the width of the playing area), divided by 2. This is the axis around which I want to center the text. Now, I subtract it by 120 pixels, which is half the maximum width of the max width. This shifts the text to the center. If it had been ObjRender_SetX(objText, 194), The area Danmakufu would have centered the text in would have been 194 pixels (the center of the playing field) + 240 pixels (maximum width).

NOTE: Different fonts may not fit within the MaxWidth, pushing letters to the next line. Render priorities above 80 do not follow the 388 pixel rule, and you must use GetStgFrameLeft+388 for the x axis and GetStgFrameTop+your desired y coordinate for the equivalent to occur.

If I wished to push the text to the left or right, I would follow a similar procedure.

===== Fonts =====

Here I will discuss installing fonts.

Here is some sample code that creates a text object. It will be referenced later.


	let objGraze = ObjText_Create();
   ObjText_SetText(objGraze, "Graze");
   ObjText_SetFontSize(objGraze,20);
   ObjText_SetFontType(objGraze,"Fairview Regular");
   ObjText_SetFontBold(objGraze, true);
   //etc

Let us consider 4 possibilities. Parentheses used to denote order of operations.

1: The font Fairview Regular is not installed on the user's computer AND (has not been installed by InstallFont OR InstallFont failed)
2: The font Fairview Regular has been installed on the user's computer AND (has not been installed by InstallFont OR InstallFont failed)
3: The font Fairview Regular is not installed on the user's computer AND (has been installed by InstallFont OR InstallFont succeeded)
4: The font Fairview Regular has been installed on the user's computer AND (has been installed by InstallFont OR InstallFont succeeded)
In the first scenario, no luck. The font will display in MS Gothic or in something weird, like Times New Roman or Arial. It will usually default to MS Gothic or whatever your computer's default fonts are.

In the second and third scenarios, the font has been installed somewhere and therefore, Danmakufu can use it without any issues.

In the last scenario, it works. Simple.

Now, how to install fonts? Use InstallFont, which returns a boolean value.


	InstallFont(GetCurrentScriptDirectory~"./../Helvetica.dfont");
   InstallFont(GetCurrentScriptDirectory~"./../Andale Mono.ttf");
   InstallFont(GetCurrentScriptDirectory~"./../Revue.ttf");
   InstallFont(GetCurrentScriptDirectory~"./../Russell Square Regular.ttf");
   InstallFont(GetCurrentScriptDirectory~"./../Fairview_Regular.otf");
   //if(!test){RaiseError("Font Install Failed");} //Test code

This code is located at the bottom of @Initialize in my system file, called by #System. It is also good to place your font installation code in the @Initialize of your package as well if you are using a package.

When installing, you give InstallFont the path to the font, NOT it's name. Now, let us utilize the test code.


	InstallFont(GetCurrentScriptDirectory~"./../Russell Square Regular.ttf");
   let test = InstallFont(GetCurrentScriptDirectory~"./../Fairview_Regular.otf");
   if(!test){RaiseError("Font Install Failed");} //Test code

This code tests whether Fairview Regular is being successfully installed. If it is NOT successfully installed, Danmakufu will raise an error popup. If it is successfully installed, the script will continue as normal. Run the test for each and every font to make sure there are no errors. Of course, when all are succeeded, comment out the test code. It's best to tell those who download your script to install the fonts anyways.

As for using the fonts, we will reference the code posted above:


	let objGraze = ObjText_Create();
   ObjText_SetText(objGraze, "Graze");
   ObjText_SetFontSize(objGraze,20);
   ObjText_SetFontType(objGraze,"Fairview Regular");
   ObjText_SetFontBold(objGraze, true);
   //etc

There is a call to use the font Fairview Regular here. When using SetFontType, address the font by its name, NOT its path. If the font has not been installed/incorrect font name, the default font or last used font or some other font will be used instead. Obviously, this is not desirable.

That's fonts in a nutshell. Make sure to test it yourself.
