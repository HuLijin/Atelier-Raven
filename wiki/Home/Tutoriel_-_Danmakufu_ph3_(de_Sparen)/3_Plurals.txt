Content-Type: text/x-zim-wiki
Wiki-Format: zim 0.4
Creation-Date: 2015-09-01T16:57:07+02:00

====== 3 Plurals ======

Plurals are completely different from 0.12m. There is no more ugly making a stage in order for a plural to have a background. **Plurals are customizable and flexible, and can be used in a large variety of situations.**

**Below is a sample plural script.**

'''
#TouhouDanmakufu[Plural]
#Title["Sample Plural Script"]
#Text["Line 1[r]Line 2"]
#Background["./Background.dnh"]
#Player["./player/KogasaOkuu/KogasaOkuuA.txt", "./player/KogasaOkuu/KogasaOkuuB.txt"]
#ScriptVersion[3]
#System["./system/System.txt"]  //system is a folder in the same folder as the plural.
 
#include"./system/Default_Effect.Txt" //Default_Effect.Txt is ph3's default effect library, moved into the system folder
#include"./FxnList.txt" //FxnList.txt is a function library
 
@Initialize{
  TPlural;
  SetPlayerLife(10); 
   /*ph3 does not have continues by default. 
  You must make your own continue system or give a large number of lives if your script is difficult.*/
}
 
@MainLoop{
  yield;
}
 
task TPlural{
  let dir=GetCurrentScriptDirectory();
  let obj=ObjEnemyBossScene_Create(); //Plurals are made from Boss Scene objects
  Sound(GetCurrentScriptDirectory~"music/boss.ogg"); 
  //Sound is a user-made function to control the music. You can find the function under Sound Objects.
  ObjEnemyBossScene_Add(obj,0,dir~"./script/SXBNS1N.txt"); //This is how you add a single to the plural. 
  ObjEnemyBossScene_Add(obj,0,dir~"./script/001RaNiSP1N.txt"); //The 0 or 1 is the lifebar to add the attack to.
  ObjEnemyBossScene_Add(obj,1,dir~"./script/SXBNS2N.txt");
  ObjEnemyBossScene_Add(obj,1,dir~"./script/003RaNiSP2N.txt");
  ObjEnemyBossScene_LoadInThread(obj); //You must load the plural inside the thread/process
  ObjEnemyBossScene_Regist(obj); //You must register the Boss Scene object
  while(!Obj_IsDeleted(obj)){ //Until the plural has been cleared, the TPlural yields.
    yield;
  }   
  CloseScript(GetOwnScriptID()); //Close the plural.
}
'''


**Please note that if you define any CommonData outside of the @Initialize, etc, they will be overridden by then next single script added to the plural.** So for example, if I SetCommonData("Attackno", 1); in SXBNS1N and SetCommonData("Attackno", 2); in SXBNS2N, the Log window will state that the value of Attackno is 2 during SXBNS1N despite the value being set to 1. **To avoid this, set common data values in @Initialize, control them from outside the Single scripts, or use separate Boss Scenes.** **This is because Boss Scenes read through the singles as they are added.** This is also the reason why plurals can detect errors within singles (outside of code blocks such as @Initialize) if Boss Scenes are used.

Note that since #Background and #System exist, you can call your own entire system and run it in the Plural without having to make a stage.
